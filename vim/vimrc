if has('mac') && has('nvim')
  " Set Python3 to homebrew's
  let python3_host_prog='/usr/local/bin/python3'
  " This is macOS only, I believe, but it fixes slow start-up for clipboard:
  let g:clipboard = {
        \'copy': {'+': 'pbcopy', '*': 'pbcopy'},
        \'paste': {'+': 'pbpaste', '*': 'pbpaste'},
        \'name': 'pbcopy', 'cache_enabled': 0
        \}
endif
let &runtimepath .= ','.expand('~/dotfiles/vim/')
let &runtimepath .= ','.expand('~/dotfiles/vim/after/')
call plug#begin('~/.vim/plugged/desktop/')
runtime bundle/common.vim
runtime bundle/desktop.vim
call plug#end()
runtime common.vim
let configs = [
  \ 'autocomplete',
  \ 'folds',
  \ 'fuzzy',
  \ 'git',
  \ 'statusline',
  \ 'syntax',
  \ 'theme',
  \ 'todo',
  \ 'writing',
\]
for file in configs
  let x = 'config/'.file.'.vim'
  execute 'runtime' x
endfor
" Disable polyglot for all the syntaxes we already use:
let g:polyglot_disabled = dotfiles#polyglot#overrides()

" Update fasd for NeoVim:
function! s:fasd_update() abort
  if empty(&buftype) || &filetype ==# 'dirvish'
    call jobstart(['fasd', '-A', expand('%:p')])
  endif
endfunction

augroup fasd
  autocmd!
  autocmd BufWinEnter,BufFilePost * call s:fasd_update()
augroup END
" # vim:foldmethod=marker
